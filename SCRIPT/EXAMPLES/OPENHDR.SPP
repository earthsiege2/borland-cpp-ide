//--------------------------------------------------------------------------
// Object Scripting
// Copyright (c) 1996 by Borland International, All Rights Reserved
//
// OPENHDR.SPP: Open Header. Opens the .h or .hpp file corresponding to the
//   current source file. Optionally creates a header file if one does not
//   exist.
//
// USE: Run script. The header file must reside in the same directory as the
//   current file. When Open Header creates header files, it uses an
//   extension based on the extension of the current file.
//
// FILES: MSG.SPP
//
// NOTES: Only supports .H or .HPP extensions.
//--------------------------------------------------------------------------
print "openhdr.spp";

//
// IDE imports.
//
import IDE;
import scriptEngine;
import editor;

//
// Load support module(s).
//
if (!scriptEngine.IsLoaded("msg.spp")) scriptEngine.Load("msg.spp");

openhdr()
{
  declare msg = new TMsg();
  declare sTmp = new String();

  // Validate current buffer.
  //
  if (!initialized(editor.TopView)) {
    msg.Info("There is no valid current buffer.");
    return;
  }
  declare curView = editor.TopView;

  // Get the fully-qualified filename less extention.
  //
  declare fileName;
  with (curView.Buffer) {
    fileName = .Drive + .Directory + .FileName;
  }

  //
  // Open the header file, looking for a .h extension first, and then a .hpp.
  // Optionally create a header file if not found.
  //
  if (FileExists(fileName + ".h")) {
    IDE.FileOpen(fileName + ".h", "EditText");
  }
  else if (FileExists(fileName + ".hpp")) {
    IDE.FileOpen(fileName + ".hpp", "EditText");
  }
  else {
    if ("Yes" == msg.YesNo("Corresponding header for " +
                           curView.Buffer.FullName +
                           " was not found. Do you want to create it?")) {

      // Create a header file based on extension of current file.
      //
      sTmp.Text = curView.Buffer.Extension;
      sTmp.Upper();
      if (sTmp.Text == ".CPP") {
        IDE.FileNew("EditText", fileName + ".hpp");
      }
      else {
        IDE.FileNew("EditText", fileName + ".h");
      }
    }
  }
}

