//--------------------------------------------------------------------------
// Object Scripting
// Copyright (c) 1996, 1997 by Borland International, All Rights Reserved
//
// LOADPROJ.SPP: Load Project. Opens the last project on start-up.
//
// USE: Autoload script on start-up.
//--------------------------------------------------------------------------
print typeid(module());

//
// IDE imports.
//
import IDE;

import "kernel32.dll" {
  int GetWindowsDirectoryA(char *, int);
}

//
// Determine last-loaded project and load it.
//
on IDE:>Started(firstTime)
{
  declare pathLength = 256;
  declare winDir = new array[pathLength];

  // Get the windows directory.
  //
  declare ret = GetWindowsDirectoryA(winDir, pathLength);
  if (!ret) {
    IDE.Message("Could not get the windows directory.");
  }

  // Get the last project from BCW5.INI.
  //
  declare iniFile = new String(winDir + "\\bcw5.ini");
  if (IDE.FileExists(iniFile.Text) != TRUE) {
    IDE.Message("Initialization file cannot be found.");
    return;
  }
  declare buffer = new EditBuffer(iniFile.Text, TRUE, TRUE);
  declare temp;
  declare i = 0;
  while (i <= buffer.Position.LastRow) {
    temp = new String(buffer.Position.Read());
    if (temp.Index("Lastproj") != 0) {
      break;
    }
    buffer.Position.GotoLine(++i);
  }
  temp = new String;
  temp.Text = (buffer.Position.RipText("=\\.:",
                                       INCLUDE_ALPHA_CHARS |
                                       INCLUDE_NUMERIC_CHARS |
                                       INCLUDE_SPECIAL_CHARS));
  declare project = new String(temp.SubString(temp.Index("=")));

  // Open the project.
  //
  IDE.ProjectOpenProject(project.Text);
  IDE.ViewProject();

  // Do Default processing.
  //
  pass(firstTime);
}

